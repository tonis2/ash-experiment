#version 450
#extension GL_ARB_separate_shader_objects : enable

// TODO: it should be better done in view space
// TODO: 3d position based clustered shading

const int TILE_SIZE = 16;
layout (constant_id = 0) const uint LIGHT_COUNT = 0U;
layout (constant_id = 1) const uint TILE_NUMS = 0U;
ivec2 VIEWPORT_SIZE = ivec2(800, 600);

struct Light {
    vec3 position;
    vec3 color;
    float intensity;
	float range;
	uint  light_type;
	float inner_cone_angle;
	float outer_cone_angle;
};

layout (binding = 2) uniform Camera {
    vec4 position;
    mat4 view;
    mat4 proj;
} camera;


#define MAX_POINT_LIGHT_PER_TILE 1023
struct LightVisiblity
{
	uint count;
	uint lightindices[MAX_POINT_LIGHT_PER_TILE];
};

layout(std140, set = 0, binding = 0) buffer writeonly TileLightVisiblities
{
    LightVisiblity light_visiblities[];
};

layout (std140, binding = 1) uniform LightBuffer {
    Light lights[LIGHT_COUNT > 0 ? LIGHT_COUNT : 1];
};


layout(set = 0, binding = 3) uniform sampler2D depth_sampler;

// vulkan ndc, minDepth = 0.0, maxDepth = 1.0
const vec2 ndc_upper_left = vec2(-1.0, -1.0);
const float ndc_near_plane = 0.0;
const float ndc_far_plane = 1.0;

struct ViewFrustum
{
	vec4 planes[6];
	vec3 points[8]; // 0-3 near 4-7 far
};

layout(local_size_x = 32) in;

shared ViewFrustum frustum;
shared uint light_count_for_tile;
shared float min_depth;
shared float max_depth;

// Construct view frustum
ViewFrustum createFrustum(ivec2 tile_id)
{

	mat4 inv_projview = inverse(camera.proj);

	vec2 ndc_size_per_tile = 2.0 * vec2(TILE_SIZE, TILE_SIZE) / VIEWPORT_SIZE;

	vec2 ndc_pts[4];  // corners of tile in ndc
	ndc_pts[0] = ndc_upper_left + tile_id * ndc_size_per_tile;  // upper left
	ndc_pts[1] = vec2(ndc_pts[0].x + ndc_size_per_tile.x, ndc_pts[0].y); // upper right
	ndc_pts[2] = ndc_pts[0] + ndc_size_per_tile;
	ndc_pts[3] = vec2(ndc_pts[0].x, ndc_pts[0].y + ndc_size_per_tile.y); // lower left

	ViewFrustum frustum;

	vec4 temp;
	for (int i = 0; i < 4; i++)
	{
		temp = inv_projview * vec4(ndc_pts[i], min_depth, 1.0);
		frustum.points[i] = temp.xyz / temp.w;
		temp = inv_projview * vec4(ndc_pts[i], max_depth, 1.0);
		frustum.points[i + 4] = temp.xyz / temp.w;
	}

	vec3 temp_normal;
	for (int i = 0; i < 4; i++)
	{
		//Cax+Cby+Ccz+Cd = 0, planes[i] = (Ca, Cb, Cc, Cd)
		// temp_normal: normal without normalization
		temp_normal = cross(frustum.points[i] - camera.position.xyz, frustum.points[i + 1] - camera.position.xyz);
		temp_normal = normalize(temp_normal);
		frustum.planes[i] = vec4(temp_normal, - dot(temp_normal, frustum.points[i]));
	}
	// near plane
	{
		temp_normal = cross(frustum.points[1] - frustum.points[0], frustum.points[3] - frustum.points[0]);
		temp_normal = normalize(temp_normal);
		frustum.planes[4] = vec4(temp_normal, - dot(temp_normal, frustum.points[0]));
	}
	// far plane
	{
		temp_normal = cross(frustum.points[7] - frustum.points[4], frustum.points[5] - frustum.points[4]);
		temp_normal = normalize(temp_normal);
		frustum.planes[5] = vec4(temp_normal, - dot(temp_normal, frustum.points[4]));
	}

	return frustum;
}

bool isCollided(Light light, ViewFrustum frustum)
{
	bool result = true;

    // Step1: sphere-plane test
	for (int i = 0; i < 6; i++)
	{
		if (dot(light.position.xyz, frustum.planes[i].xyz) + frustum.planes[i].w  < - light.range)
		{
			result = false;
			break;
		}
	}

    if (!result)
    {
        return false;
    }

    // Step2: bbox corner test (to reduce false positive)
    vec3 light_bbox_max = light.position.xyz + vec3(light.range);
    vec3 light_bbox_min = light.position.xyz - vec3(light.range);
    int probe;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].x > light_bbox_max.x)?1:0); if( probe==8 ) return false;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].x < light_bbox_min.x)?1:0); if( probe==8 ) return false;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].y > light_bbox_max.y)?1:0); if( probe==8 ) return false;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].y < light_bbox_min.y)?1:0); if( probe==8 ) return false;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].z > light_bbox_max.z)?1:0); if( probe==8 ) return false;
    probe=0; for( int i=0; i<8; i++ ) probe += ((frustum.points[i].z < light_bbox_min.z)?1:0); if( probe==8 ) return false;

	return true;
}

void main()
{
	ivec2 tile_id = ivec2(gl_WorkGroupID.xy);
	uint tile_index = tile_id.y * TILE_NUMS + tile_id.x;

	// TODO: depth culling???

	if (gl_LocalInvocationIndex == 0)
	{
		min_depth = 1.0;
		max_depth = 0.0;

		for (int y = 0; y < TILE_SIZE; y++)
		{
			for (int x = 0; x < TILE_SIZE; x++)
			{
				vec2 sample_loc = (vec2(TILE_SIZE, TILE_SIZE) * tile_id + vec2(x, y) ) / VIEWPORT_SIZE;
				float pre_depth = texture(depth_sampler, sample_loc).x;
				min_depth = min(min_depth, pre_depth);
				max_depth = max(max_depth, pre_depth); //TODO: parallize this
			}
		}

		if (min_depth >= max_depth)
		{
			min_depth = max_depth;
		}

		frustum = createFrustum(tile_id);
		light_count_for_tile = 0;
	}

	barrier();

	for (uint i = gl_LocalInvocationIndex; i < LIGHT_COUNT && light_count_for_tile < MAX_POINT_LIGHT_PER_TILE; i += gl_WorkGroupSize.x)
	{
		if (isCollided(lights[i], frustum))
		{
			uint slot = atomicAdd(light_count_for_tile, 1);
			if (slot >= MAX_POINT_LIGHT_PER_TILE) {break;}
			light_visiblities[tile_index].lightindices[slot] = i;
		}
	}

	barrier();

	if (gl_LocalInvocationIndex == 0)
	{
		light_visiblities[tile_index].count = min(MAX_POINT_LIGHT_PER_TILE, light_count_for_tile);
	}
}